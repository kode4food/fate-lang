import nodeunit

from test import equalTo, throws

def guardOnly(test)
  def testFunc(x) where x > 10
    x * 10
  end

  testFunc(12)     | equalTo(120)
  (-> testFunc(9)) | throws
  test.done()
end

def guardAndDefault(test)
  def testFunc(x)
    x * 5
  end

  def testFunc(self > 5 as x) where x > 10
    x * 10
  end

  testFunc(12) | equalTo(120)
  testFunc(9)  | equalTo(45)
  test.done()
end

def anonymousPatternArgument(test)
  def testFunc(x)
    x * 5
  end

  def testFunc(self > 5)
    "hello"
  end

  testFunc(5) | equalTo(25)
  testFunc(6) | equalTo('hello')
  test.done()
end

def noArguments(test)
  let a = 99

  def testFunc
    'fell through'
  end

  def testFunc where a = 99
    'guard worked'
  end

  testFunc() | equalTo('guard worked')
  test.done()
end

export let tests = nodeunit.testCase({
  "Functions with Guards Only": guardOnly,
  "Functions with Guard and Default": guardAndDefault,
  "Functions with Anonymous Patterns": anonymousPatternArgument,
  "Functions with No Arguments": noArguments
})
